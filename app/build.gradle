/*
 * This file is part of Grocy Android.
 *
 * Grocy Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Grocy Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Grocy Android. If not, see http://www.gnu.org/licenses/.
 *
 * Copyright (c) 2020-2024 by Patrick Zedler and Dominic Zedler
 * Copyright (c) 2024-2026 by Patrick Zedler
 */

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.navigation.safeargs)
}

android {
    namespace = 'xyz.zedler.patrick.grocy'
    compileSdk = 36

    defaultConfig {
        applicationId = "xyz.zedler.patrick.grocy"
        minSdk = 23
        targetSdk = 36
        versionCode = 64
        versionName = "3.8.3"
        resourceConfigurations += ['ca', 'cs', 'de', 'en', 'es', 'et', 'fr', 'hu', 'it', 'iw', 'ja',
                                   'nb', 'nl', 'pl', 'pt-rBR', 'pt-rPT', 'ru', 'sk', 'sv', 'uk',
                                   'zh-rCN', 'zh-rTW']
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode = true
                removeUnusedResources = true
                obfuscate = false
                optimizeCode = true
                proguardFiles {
                    getDefaultProguardFile('proguard-android-optimize.txt')
                    'proguard-rules.pro'
                }
            }
            debuggable = false
        }
        debug {
            applicationIdSuffix = '.debug'
        }
    }

    buildFeatures {
        viewBinding = true
        dataBinding = true
        buildConfig = true
    }

    lint {
        abortOnError = false
        disable = ['MissingTranslation']
    }

    bundle {
        storeArchive {
            enable = true
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_21
    }

    dependenciesInfo {
        includeInApk = false
    }

    packagingOptions {
        jniLibs.useLegacyPackaging = false
    }
}

dependencies {
    implementation(libs.appcompat)
    implementation(libs.fragment)
    implementation(libs.preference)
    implementation(libs.browser)
    implementation(libs.webkit)
    implementation(libs.lifecycle.viewmodel.savedstate)
    implementation(libs.lifecycle.viewmodel)
    implementation(libs.paging)
    implementation(libs.kotlinx.serialization.json)
    // Navigation framework for easy navigation control using XML
    implementation(libs.navigation.fragment)
    implementation(libs.navigation.ui)
    // Android Room for database and RxJava for Async access of database
    implementation(libs.room.runtime)
    implementation(libs.room.rxjava3)
    annotationProcessor(libs.room.compiler)
    implementation(libs.rxandroid)
    // Material views
    implementation(libs.material)
    implementation(libs.calendar)
    implementation(libs.flexbox)
    // Volley for all general network requests and Glide for image loading
    // Conscrypt for new TLS support on old Android devices
    implementation(libs.volley)
    implementation(libs.glide)
    implementation(libs.conscrypt)
    implementation(libs.websocket.client)
    // GSON for converting JSON responses to Java objects
    implementation(libs.gson)
    // FuzzyWuzzy for fuzzy search in product names and autocompletion
    implementation(libs.fuzzywuzzy)
    // Work for daily notifications
    implementation(libs.work.runtime)
    // ZXing for barcode scanning
    implementation(libs.zxing.core)
    implementation(libs.zxing.android.embedded)
    // For image rotation
    implementation(libs.exifinterface)
    // https://github.com/journeyapps/zxing-android-embedded#option-2-desugaring-advanced
    // prevents bug https://github.com/patzly/grocy-android/issues/425
    coreLibraryDesugaring(libs.desugar)
}
