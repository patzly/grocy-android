/*
 * This file is part of Grocy Android.
 *
 * Grocy Android is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Grocy Android is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Grocy Android. If not, see http://www.gnu.org/licenses/.
 *
 * Copyright (c) 2020-2022 by Patrick Zedler and Dominic Zedler
 */

plugins {
    id 'com.android.application'
    id 'androidx.navigation.safeargs'
}

android {
    namespace 'xyz.zedler.patrick.grocy'
    compileSdkVersion 33
    buildToolsVersion '33.0.0'

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    lint {
        abortOnError false
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "xyz.zedler.patrick.grocy"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 42
        versionName "2.3.0"
        resConfigs 'cs', 'de', 'en', 'es', 'fr', 'hu', 'it', 'iw', 'nb', 'nl', 'pl', 'pt-rPT', 'ru',
                'sk', 'sv', 'uk', 'zh-rCN', 'zh-rTW'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate false
                optimizeCode true
                proguardFiles {
                    getDefaultProguardFile('proguard-android-optimize.txt')
                    'proguard-rules.pro'
                }
            }
            debuggable false
        }
    }

    bundle {
        storeArchive {
            enable true
        }
    }
    
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility 11
        targetCompatibility 11
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.0' // set version to 1.6.0 as soon as stable version is released
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // Navigation framework for easy navigation control using XML
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    // Android Room for database and RxJava for Async access of database
    implementation 'androidx.room:room-runtime:2.4.3'
    implementation 'androidx.room:room-rxjava3:2.4.3'
    annotationProcessor 'androidx.room:room-compiler:2.4.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    // Nice material views
    implementation 'com.google.android.material:material:1.8.0-alpha03'

    // Volley for all general network requests and Glide for image loading
    // Netcipher for Tor support and Conscrypt for new TLS support on old Android devices
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'info.guardianproject.netcipher:netcipher:2.1.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'

    // GSON for converting JSON responses to Java objects
    implementation 'com.google.code.gson:gson:2.10'

    // FuzzyWuzzy for fuzzy search in product names and autocompletion
    implementation 'me.xdrop:fuzzywuzzy:1.4.0'

    // Work for daily notifications
    implementation 'androidx.work:work-runtime:2.7.1'

    implementation 'androidx.webkit:webkit:1.5.0'

    // ZXing for barcode scanning
    //noinspection GradleDependency
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    // https://github.com/journeyapps/zxing-android-embedded#option-2-desugaring-advanced
    // prevents bug https://github.com/patzly/grocy-android/issues/425
    //noinspection GradleDependency
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}